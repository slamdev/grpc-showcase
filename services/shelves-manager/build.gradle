plugins {
    id 'idea'
    id 'java'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'com.google.protobuf' version '0.8.12'
}

group 'com.github.slamdev.grpcshowcase'

sourceCompatibility = JavaVersion.VERSION_13
targetCompatibility = JavaVersion.VERSION_13

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR6'
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.3.1.RELEASE'
        mavenBom 'io.grpc:grpc-bom:1.30.2'
    }
    dependencies {
        dependency 'com.google.protobuf:protobuf-java:3.12.2'
        dependency 'net.devh:grpc-server-spring-boot-starter:2.9.0.RELEASE'
    }
}

configurations {
    compile.exclude module: 'junit'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'net.devh:grpc-server-spring-boot-starter' exclude module: 'grpc-netty-shaded'
    implementation 'com.google.protobuf:protobuf-java'
    implementation 'io.grpc:grpc-protobuf'
    implementation 'io.grpc:grpc-stub' // ?
    implementation 'io.grpc:grpc-netty'
    testImplementation 'org.springframework.boot:spring-boot-starter-test' exclude module: 'junit-vintage-engine'
    testImplementation 'io.grpc:grpc-testing'
}

repositories {
    jcenter()
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.30.2'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
        }
    }
}

task resolveDependencies {
    doLast {
        Set<Configuration> configurations = project.buildscript.configurations + project.configurations
        configurations.findAll { c -> c.canBeResolved }.forEach { c -> c.resolve() }
    }
}
